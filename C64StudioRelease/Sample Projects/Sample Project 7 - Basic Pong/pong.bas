10 W=53280:POKEW,0:POKEW+1,0:GOSUB310:PRINT"{clr}":FORI=1TO40:PRINT"";:NEXT
20 FORI=1TO20:PRINT"{black}X{left}{left}{down}Z";:NEXT:FORI=1TO40:PRINT"{white}";:NEXT:Z=B-19:X=B+58
30 M=X:N=Z:GETA$:GOSUB60:B=B+D:P=PEEK(B):IFP<>32THEN150
40 PRINT"{home}"B$" "C$":"P1"  "B$" "D$":"P2:FORI=0TO80STEP40:POKEX+I,160
50 POKEZ+I,160:NEXT:POKEB,160:C=B:FORI=1TO35:NEXT:POKEC,32:GOTO30
60 IFA$="Q"THENZ=Z-40:POKEN+80,32
70 IFA$="A"THENZ=Z+40:POKEN,32
80 IFA$="O"THENX=X-40:POKEM+80,32
90 IFA$="L"THENX=X+40:POKEM,32
100 IFZ<1105THENZ=1105
110 IFZ+80>1865THENZ=1785
120 IFX<1142THENX=1142
130 IFX+80>1902THENX=1822
140 RETURN
150 IFP<>67ANDP<>70THEN200
160 IFD=-39THEND=41:GOTO30
170 IFD=-41THEND=39:GOTO30
180 IFD=41THEND=-39:GOTO30
190 IFD=39THEND=-41:GOTO30
200 IFP<>160THEN250
210 IFD=41THEND=39:GOTO30
220 IFD=-39THEND=-41:GOTO30
230 IFD=-41THEND=-39:GOTO30
240 IFD=39THEND=41:GOTO30
250 IFP=24THENP2=P2+10
260 IFP=26THENP1=P1+10
270 IFP1>90THENS$=C$:GOTO300
280 IFP2>90THENS$=D$:GOTO300
290 B=K:GOTO30
300 PRINT"{clr}{down}{down}{down}{down}{down}{right}{right}{right}{right}{right}{right}{right}{right}{right}{right}{right}{right}{right}{right}{right}GAME  OVER{down}{down}{left}{left}{left}{left}{left}{left}{left}{left}{left}{left}{left}{left}{left}{left}{left}WINNER IS "B$" "S$:END
310 B$="PLAYER":C$="ONE":D$="TWO":B=1524:D=-39:K=B
320 PRINT"{clr}{white}{rvson}   PING - BY PINGO/CIVITAS/DIXN RIAH    ":PRINTB$" "C$;
330 PRINT": {rvson}Q{rvsoff} UP {rvson}A{rvsoff} DOWN.":PRINTB$" "D$": {rvson}O{rvsoff} UP {rvson}L{rvsoff} DOWN.":PRINT"FIRST TO 100 WINS."
340 GETZ$:IFZ$=""THEN340
350 RETURN
