#RetroDevStudio.MetaData.BASIC:2049,Tuned Simons' BASIC,lowercase,10,10
1 REM *********************************
2 REM *       TESTBILDGENERATOR       *
3 REM *********************************
4 REM *    DIETER LAUES 64'ER 1/87    *
5 REM ********************************* 
6 REM *     TSB: A. DETTKE  V1.03     *
7 REM *********************************
 
8 GRAPHICS:ENVELOPE1,8,8,8,8:VOL15:CHECK
10 KEY OFF:S=SOUND:WL=192:CR=174:SL=93
 
20 REPEAT 
# OBERFLAECHE: VERZIERUNGEN
25 NRM:CLS:COLOR3,3,2:PRINTAT(0,13)DUP(CHR$(WL),15)" ESTBILD "CHR$(WL)CHR$(CR);
30 FILL1,39,1,20,SL,2:A$=" TSB ":L=LEN(A$)+1
32 FORA=2TOL:FILLA,39,1,1,ASC(MID$(A$,A-1)),0:NEXT:COLOR,0
# MENUE
35 PRINTAT(1,1)"1 - ROT"
40 PRINTAT(LIN,1)"2 - GRUEN"
45 PRINTAT(LIN,1)"3 - BLAU"
50 PRINTAT(LIN+1,1)"4 - ITTER"
55 PRINTAT(LIN+1,1)"5 - ARBBALKEN"
60 PRINTAT(LIN+1,1)"6 - ULTIBURST"
65 PRINTAT(LIN+1,1)"7 - 50Z-PRUNG"
70 PRINTAT(LIN+1,1)"8 - 2-MPULS"
75 PRINTAT(LIN+1,1)"9 - ARBTESTBILD"
80 PRINTAT(LIN+1,1)DUP("-",37)
85 PRINTAT(LIN,1)"1  EGELTON         1000 Z 7/8:"
90 PRINTAT(LIN,1)"3  WEITON-ENNUNG   274 Z LAUTER/"
95 PRINTAT(LIN,1)"5  TEREO-ENNUNG    117 Z LEISER"
96 PRINTAT(LIN,1)DUP("-",37):COLOR,1
97 PRINTAT(LIN+1,1)"";:COLOR,0:PRINT" EST-NDE ";AT(LIN,20)"";
98 COLOR,1:PRINT"=8";:COLOR,0:PRINT" ROGRAMM-NDE"
# TASTEN- UND F-ABFRAGE (35: =8)
100 A$="":POKE198,0:REPEAT:A=INKEY+19:IFA=19THENGETA$:A=VAL(A$)
105 UNTILA>0:IFA=35THENA=19
# F-TASTE? JA: INS SUB
106 IFA>19ANDA<28THENGOSUB230
# ZIFFER? (F-TASTE RUTSCHT DURCH)
107 IFA<>19THENDO
110 ON A GOSUB 295,300,305,315,330,370,410,440,460
111 DONE
115 UNTILA=19
# ENDSEQUENZ
116 KEY ON:NRM:CLS:PRINTAT(12,0)"";:CENTER"IS ZUM NAECHSTEN AL!"
117 PRINTAT(21,0)"":END

120 REM *******************************
125 REM *        UNTERPROGRAMME       *
130 REM *******************************

135 PROC BURST
140 FORN=ATOBSTEPC:BLOCKN,0,N+D,183,1:NEXT
155 LOW COL2,0,0:TEXT X,190,A$:HI COL
156 END PROC

160 PROC BARS
161 FORN=ATOBSTEPC
162 BLOCKN,112,N+D,127,1:NEXT
163 END PROC

# LOESCHEN MIT MEMCLR STATT MIT BLOCK
170 PROC FASTBLOCK
171 AD=$E000+Y*40+X:N=H/8
172 FORA=0TON-1:MEMCLRAD+A*320,B,EX:NEXT
174 END PROC

175 PROC RASTER
180 CLS:COLOR0,0:HIRES1,0
185 FORN=8TO320STEP 20
190 LINEN,0,N,199,1:NEXT
195 FORN=8TO199STEP 20
200 LINE0,N,319,N,1:NEXT
205 END PROC

210 PROC FARBE
215 CLS:COLORA,A:VOL8
220 END PROC

# TASTENABFRAGE MIT ENTPRELLUNG (WARTEN SOLANGE 197<>64)
225 PROC TASTEN
230 L=8:REPEAT:GETA$:IFA$=""THENREPEAT:A=INKEY:UNTILPEEK(197)=64
235 IFA$="X"THENA=16
240 IFA=1THENT=$4284:.TOENE
245 IFA=3THENT=$123A:.TOENE
250 IFA=5THENT=$07C8:.TOENE
255 IFA=7THENL=L+1:IFL>15THENL=15
260 IFA=8THENL=L-1:IFL<0THENL=0
265 VOLL
266 UNTILA>8
268 RETURN

# TOENE EIN- UND AUSSCHALTEN (E ALS SCHALTER)
270 PROC .TOENE
275 ENVELOPE1,0,0,15,0
280 A=16:E=1-E:IFE=1THENWAVE1,$11:SOUND1,T:ELSEWAVE1,0
285 END PROC

290 REM ******* GRUNDFARBEN **********
295 A=2:FARBE:CALL TASTEN:REM ROT
300 A=5:FARBE:CALL TASTEN:REM GRUEN
305 A=6:FARBE:CALL TASTEN:REM BLAU

310 REM ********** GITTER ************
315 RASTER
320 CALL TASTEN

325 REM ******** FARBBALKEN **********
330 CLS:COLOR 15,0
335 FILL0, 0,5,25,160,1
340 FILL0, 5,5,25,160,7
345 FILL0,10,5,25,160,3
350 FILL0,15,5,25,160,5
355 FILL0,20,5,25,160,4
356 FILL0,25,5,25,160,2
357 FILL0,30,5,25,160,6
358 FILL0,35,5,25,160,0
360 CALL TASTEN

365 REM ******* MULTIBURST **********
370 CLS:COLOR0,0:HIRES1,0
375 A=0  :B=70 :C=16:D=7:X=5  :A$="0,5 Z":BURST
380 A=76 :B=128:C=8 :D=3:X=80 :A$="1 Z":BURST
385 A=132:B=192:C=6 :D=2:X=135:A$="1,3 Z":BURST
390 A=198:B=256:C=4 :D=1:X=205:A$="2 Z":BURST
395 A=258:B=318:C=2 :D=0:X=265:A$="4 Z":BURST
400 CALL TASTEN

405 REM ******* 50HZ-SPRUNG **********
410 CLS:COLOR0,0:FILL4,0,40,15,160,1
430 CALL TASTEN

435 REM ******* 2T-IMPULS ***********
440 CLS:COLOR0,0:HIRES1,0:LINE160,0,160,199,1
445 CALL TASTEN

# GESCHWINDIGKEITS-OPTIMIERT (AM LANGSAMSTEN IST "RASTER"):
450 REM ****** TESTBILD *************
460 RASTER:X=64:Y=24:H=152:B=192:RECX-1,Y-1,B+1,H+1,1:EX=$FF:FASTBLOCK
461 £M:£K,159,100,100,100,1:LINE149,199,167,199,1
# FARBEN:
465 POKE648,$C0:FCHR3,8,3,5,$10
470 FCHR3,11,3,5,$70:FCHR3,14,3,5,$30:FCHR3,17,3,5,$50:FCHR3,20,3,5,$40
475 FCHR3,23,3,5,$20:FCHR3,26,3,5,$60:FCHR3,29,3,5,$00
485 FCHR8,8,5,4,$00:FCHR8,13,5,4,$B0:FCHR8,18,5,4,$C0:FCHR8,23,5,4,$F0
490 BLOCK97,97,222,111,0:BLOCK64,113,255,127,0:BLOCK159,129,169,142,0
500 LINE160,142,170,129,1:PAINT162,141,1
560 FCHR18,8,16,2,$20:FCHR20,8,16,2,$60:FCHR18,24,8,4,$C0
565 LINE223,144,223,175,0:POKE648,4
# MULTIBURST:
700 A=86 :B=125:C=8:D=3:BARS
705 A=125:B=160:C=6:D=2:BARS
710 A=160:B=195:C=4:D=1:BARS
715 A=196:B=230:C=2:D=0:BARS
720 TEXT 113,101,"-ESTBILD"
750 CALL TASTEN
